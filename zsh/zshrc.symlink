# Shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Set default editor
export EDITOR=nvim

# Stash environment variables and other local config in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# Display helpers
function info() {
  echo "Loading $(echo $1 | rev | cut -d'/' -f1-2 | rev)"
}

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  info $file
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  info $file
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  info $file
  source $file
done

unset config_files